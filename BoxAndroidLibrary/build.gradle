buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:0.5.+'
	}
}

apply plugin: 'android-library'
apply plugin: 'maven'

group = 'com.box.android'
archivesBaseName = 'sdk';
version = '1.0.0'

repositories {
	mavenCentral()
	maven { url releaseRepo }
	maven { url snapshotRepo }
}

configurations {
	archives {
		extendsFrom configurations.default
	}
}

uploadArchives {
	configuration = configurations.archives
	repositories.mavenDeployer {
		repository(url: releaseRepo) {
			authentication(userName: username,
				password: password)
		}
		snapshotRepository(url: snapshotRepo) {
			authentication(userName: username,
				password: password)
		}

		pom.project {
			name 'Box Android SDK v1'
			packaging 'aar'
			description 'Android SDK for Box v1'
			url 'https://github.com/box/box-android-sdk'

			scm {
				url 'scm:git@github.com:box/box-android-sdk.git'
				connection 'scm:git@github.com:box/box-android-sdk.git'
				developerConnection 'scm:git@github.com:box/box-android-sdk.git'
			}

			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}

			developers {
				developer {
					id 'gcurtis'
					name 'Greg Curtis'
					email 'gcurtis@box.com'
				}
			}
		}
	}
}

dependencies {
	compile 'org.apache.httpcomponents:httpmime:4.2.5'
	compile 'org.apache.james:apache-mime4j:0.7.2'
	compile files('libs/gson-renamed-2.2.2.jar')
}

android {
	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			res.srcDirs = ['res']
		}
	}
	buildToolsVersion "18.0.1"
	compileSdkVersion 18
}
